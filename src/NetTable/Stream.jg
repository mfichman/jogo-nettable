# Copyright (c) 2010 Matt Fichman
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, APEXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

Msg < Value {
    # Represents the message format used by NetTable for sending primitives
    # (tables, strings, ints, floats, etc)
    id Int # ID of the table that was modified
    key String # Key that was modified within the table
    val Val # The modified value for 'key'

    @init(pid Int, pkey String, pval Val) {
        id = pid
        key = pkey
        val = pval
    }

    str?() String {
        # Print a human-readable representation of this message
        if not val {
            ret "id=#{id} key=#{key} val=nil"
        } else if val is String {
            str String = val
            ret "id=#{id} key=#{key} val=#{str}"   
        } else if val is Int {
            str String = val
            int Int = val
            ret "id=#{id} key=#{key} val=#{int}"   
        } else if val is Table {
            str String = val
            ret "id=#{id} key=#{key} val=[table]"   
        } else {
            str String = val
            Os::panic('Unknown value')
            ret nil
        }
    }
}

Stream < Object {
    # Handles serializing/deserializing NetTable values.  Basically, a
    # NetTable::Stream is a wrapper around Stream that uses the NetTable
    # protocol to serialize tables, strings, integers, etc. 
    stream embedded Io::Stream
    input = Hash[Id,Table]() # Used to parse incoming IDs from the peer
    output = Hash[Table,Id]() # Used to send IDs to the peer
    id = 2

    @init(pstream Io::Stream) {
        # Initializes a new NetTable::Stream from an Io::Stream. 
        stream = pstream
    }

    gettable() Table {
        # Reads a NetTable table from the stream.  Each NetTable is represented
        # by an ID.  If the ID given has not been seen before, and is only just
        # larger than the previous ID, then a new table was created by the
        # remote peer. 
        tid = Id(self.getib())
        table var = input[tid]
        if not table {
            table = Table()
            input[tid] = table
        }
        ret table
    }

    getval() Val {
        # Read a NetTable value from the stream.  Depending on the type prefix,
        # this will be either a string, integer, float, nil, or table.
        match self.getbb() {
        with 's'b: ret self.getsb() # Binary string
        with 'i'b: ret self.getib() # 64-bit integer
        #with 'f'b: ret self.getfb() # 64-bit floating-point
        with 'n'b: ret nil # Nil value (delete)
        with 't'b: ret self.gettable() # Table value, defined by table ID
        with *: 
            Os::panic('Invalid type')
            ret nil
        }
    }

    getmsg() Msg {
        # Reads the next NetTable message from the stream.  The message is not
        # considered valid if the stream is not in the "OK" state after this
        # method returns. 
        msg = Msg(self.getib(), self.getsb(), self.getval())

        Io::println("get #{msg}")
        ret msg
    }     

    puttable(table Table) {
        # Writes a table to the stream.  This function writes the table ID,
        # then writes the table keys if they also need to be sent.
        tid var = output[table]
        if not tid {
            # FixMe: Stream out the entire table?
            tid = Id(id)
            id = id+1
        } 
        self.putib(tid.val)        
    }

    putval(value Val) {
        # Writes a NetTable value to the stream.  Each value is prefixed with a
        # character code that identifies the type of the value.
        if not value {
            self.putbb('n'b)
        } else if value is String {
            self.putbb('s'b)
            self.putsb(value)
        #} else if value is Float {
        #    self.putbb('f'b)
        #    self.putfb(value)
        } else if value is Int {
            self.putbb('i'b)
            self.putib(value)
        } else if value is Table {
            self.putbb('t'b)
            self.puttable(value)
        } else {
            Os::panic('Invalid type')  
        }
    }

    putmsg(msg Msg) {
        # Writes a NetTable message to the stream.  The message was not written
        # successfully if the stream is not in the OK state after this method
        # returns.
        self.putib(msg.id)
        self.putsb(msg.key)
        self.putval(msg.val)

        Io::println("put #{msg}")
    }
}
